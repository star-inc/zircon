# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

##########################################
# Though under //zircon, this build file #
# is meant to be used in the Fuchsia GN  #
# build.                                 #
# See fxb/36139.                         #
##########################################

assert(!defined(zx) || zx != "/",
       "This file can only be used in the Fuchsia GN build.")

import("//build/config/fuchsia/rules.gni")
import("//build/test.gni")
import("//build/test/test_package.gni")
import("//build/unification/images/migrated_manifest.gni")

test("platform-bus") {
  configs += [ "//build/unification/config:zircon-migrated" ]
  sources = [ "main.cc" ]
  deps = [
    "//sdk/fidl/fuchsia.sysinfo:fuchsia.sysinfo_llcpp",
    "//zircon/public/lib/fbl",
    "//zircon/public/lib/fdio",
    "//zircon/public/lib/libzbi",
    "//zircon/public/lib/zx",
    "//zircon/public/lib/zxtest",
    "//zircon/system/ulib/ddk",
    "//zircon/system/ulib/devmgr-integration-test",
    "//zircon/system/ulib/devmgr-launcher",
  ]
}

simple_drivers = [
  "child-1",
  "child-2",
  "child-2.proxy",
  "child-3",
  "parent",
  "composite",
]

group("test") {
  testonly = false
  deps = [
    ":test-audio-codec",
    ":test-board",
    ":test-clock",
    ":test-gpio",
    ":test-i2c",
    ":test-power",
    ":test-pwm",
    ":test-spi",
  ]
  foreach(driver, simple_drivers) {
    deps += [ ":test-$driver" ]
  }
}

driver_module("test-board") {
  defines = [ "_ALL_SOURCE" ]
  configs += [ "//build/config/fuchsia:enable_zircon_asserts" ]
  configs -= [ "//build/config/fuchsia:no_cpp_standard_library" ]
  configs += [ "//build/config/fuchsia:static_cpp_standard_library" ]
  configs += [ "//build/unification/config:zircon-migrated" ]
  sources = [
    "test-audio-codec.cc",
    "test-board.cc",
    "test-clock.cc",
    "test-gpio.cc",
    "test-i2c.cc",
    "test-power.cc",
    "test-pwm.cc",
    "test-spi.cc",
    "test.cc",
  ]
  deps = [
    "//sdk/banjo/ddk.protocol.platform.bus",
    "//sdk/banjo/ddk.protocol.platform.device",
    "//src/devices/lib/amlogic",
    "//src/devices/lib/driver",
    "//zircon/public/lib/fbl",
    "//zircon/system/ulib/ddk",
    "//zircon/system/ulib/ddktl",
  ]
}

driver_module("test-gpio") {
  defines = [ "_ALL_SOURCE" ]
  configs += [ "//build/config/fuchsia:enable_zircon_asserts" ]
  configs -= [ "//build/config/fuchsia:no_cpp_standard_library" ]
  configs += [ "//build/config/fuchsia:static_cpp_standard_library" ]
  configs += [ "//build/unification/config:zircon-migrated" ]
  sources = [ "test/gpio.cc" ]
  deps = [
    "//sdk/banjo/ddk.protocol.gpio",
    "//sdk/banjo/ddk.protocol.gpioimpl",
    "//sdk/banjo/ddk.protocol.platform.bus",
    "//sdk/banjo/ddk.protocol.platform.device",
    "//src/devices/lib/driver",
    "//zircon/public/lib/fbl",
    "//zircon/public/lib/zx",
    "//zircon/system/ulib/ddk",
    "//zircon/system/ulib/ddktl",
  ]
}

driver_module("test-power") {
  defines = [ "_ALL_SOURCE" ]
  configs += [ "//build/config/fuchsia:enable_zircon_asserts" ]
  configs -= [ "//build/config/fuchsia:no_cpp_standard_library" ]
  configs += [ "//build/config/fuchsia:static_cpp_standard_library" ]
  configs += [ "//build/unification/config:zircon-migrated" ]
  sources = [ "test/power.cc" ]
  deps = [
    "//sdk/banjo/ddk.protocol.platform.bus",
    "//sdk/banjo/ddk.protocol.platform.device",
    "//sdk/banjo/ddk.protocol.power",
    "//sdk/banjo/ddk.protocol.powerimpl",
    "//src/devices/lib/driver",
    "//zircon/public/lib/fbl",
    "//zircon/public/lib/zx",
    "//zircon/system/ulib/ddk",
    "//zircon/system/ulib/ddktl",
  ]
}

driver_module("test-clock") {
  defines = [ "_ALL_SOURCE" ]
  configs += [ "//build/config/fuchsia:enable_zircon_asserts" ]
  configs -= [ "//build/config/fuchsia:no_cpp_standard_library" ]
  configs += [ "//build/config/fuchsia:static_cpp_standard_library" ]
  configs += [ "//build/unification/config:zircon-migrated" ]
  sources = [ "test/clock.cc" ]
  deps = [
    "//sdk/banjo/ddk.protocol.clock",
    "//sdk/banjo/ddk.protocol.clockimpl",
    "//sdk/banjo/ddk.protocol.platform.bus",
    "//sdk/banjo/ddk.protocol.platform.device",
    "//src/devices/lib/driver",
    "//zircon/public/lib/fbl",
    "//zircon/public/lib/zx",
    "//zircon/system/ulib/ddk",
    "//zircon/system/ulib/ddktl",
  ]
}

driver_module("test-i2c") {
  defines = [ "_ALL_SOURCE" ]
  configs += [ "//build/config/fuchsia:enable_zircon_asserts" ]
  configs -= [ "//build/config/fuchsia:no_cpp_standard_library" ]
  configs += [ "//build/config/fuchsia:static_cpp_standard_library" ]
  configs += [ "//build/unification/config:zircon-migrated" ]
  sources = [ "test/i2c.cc" ]
  deps = [
    "//sdk/banjo/ddk.protocol.i2c",
    "//sdk/banjo/ddk.protocol.i2cimpl",
    "//sdk/banjo/ddk.protocol.platform.bus",
    "//sdk/banjo/ddk.protocol.platform.device",
    "//src/devices/lib/driver",
    "//zircon/public/lib/fbl",
    "//zircon/public/lib/zx",
    "//zircon/system/ulib/ddk",
    "//zircon/system/ulib/ddktl",
  ]
}

driver_module("test-spi") {
  defines = [ "_ALL_SOURCE" ]
  configs += [ "//build/config/fuchsia:enable_zircon_asserts" ]
  configs -= [ "//build/config/fuchsia:no_cpp_standard_library" ]
  configs += [ "//build/config/fuchsia:static_cpp_standard_library" ]
  configs += [ "//build/unification/config:zircon-migrated" ]
  sources = [ "test/spi.cc" ]
  deps = [
    "//sdk/banjo/ddk.protocol.platform.bus",
    "//sdk/banjo/ddk.protocol.platform.device",
    "//sdk/banjo/ddk.protocol.spi",
    "//sdk/banjo/ddk.protocol.spiimpl",
    "//src/devices/lib/driver",
    "//zircon/public/lib/fbl",
    "//zircon/public/lib/zx",
    "//zircon/system/ulib/ddk",
    "//zircon/system/ulib/ddktl",
  ]
}

driver_module("test-audio-codec") {
  defines = [ "_ALL_SOURCE" ]
  configs += [ "//build/config/fuchsia:enable_zircon_asserts" ]
  configs -= [ "//build/config/fuchsia:no_cpp_standard_library" ]
  configs += [ "//build/config/fuchsia:static_cpp_standard_library" ]
  configs += [ "//build/unification/config:zircon-migrated" ]
  sources = [ "test/audio-codec.cc" ]
  deps = [
    "//sdk/banjo/ddk.protocol.codec",
    "//sdk/banjo/ddk.protocol.platform.bus",
    "//sdk/banjo/ddk.protocol.platform.device",
    "//src/devices/lib/driver",
    "//zircon/public/lib/fbl",
    "//zircon/public/lib/zx",
    "//zircon/system/ulib/ddk",
    "//zircon/system/ulib/ddktl",
  ]
}

driver_module("test-pwm") {
  defines = [ "_ALL_SOURCE" ]
  configs += [ "//build/config/fuchsia:enable_zircon_asserts" ]
  configs -= [ "//build/config/fuchsia:no_cpp_standard_library" ]
  configs += [ "//build/config/fuchsia:static_cpp_standard_library" ]
  configs += [ "//build/unification/config:zircon-migrated" ]
  sources = [ "test/pwm.cc" ]
  deps = [
    "//sdk/banjo/ddk.protocol.platform.bus",
    "//sdk/banjo/ddk.protocol.platform.device",
    "//sdk/banjo/ddk.protocol.pwm",
    "//src/devices/lib/driver",
    "//zircon/public/lib/fbl",
    "//zircon/public/lib/zx",
    "//zircon/system/ulib/ddk",
    "//zircon/system/ulib/ddktl",
  ]
}

foreach(driver, simple_drivers) {
  driver_module("test-$driver") {
    defines = [ "_ALL_SOURCE" ]
    configs += [ "//build/config/fuchsia:enable_zircon_asserts" ]
    configs -= [ "//build/config/fuchsia:no_cpp_standard_library" ]
    configs += [ "//build/config/fuchsia:static_cpp_standard_library" ]
    configs += [ "//build/unification/config:zircon-migrated" ]
    sources = [ "test/$driver.c" ]
    deps = [
      "//sdk/banjo/ddk.protocol.clock",
      "//sdk/banjo/ddk.protocol.codec",
      "//sdk/banjo/ddk.protocol.composite",
      "//sdk/banjo/ddk.protocol.gpio",
      "//sdk/banjo/ddk.protocol.i2c",
      "//sdk/banjo/ddk.protocol.platform.device",
      "//sdk/banjo/ddk.protocol.power",
      "//sdk/banjo/ddk.protocol.pwm",
      "//sdk/banjo/ddk.protocol.spi",
      "//src/devices/i2c/lib/device-protocol-i2c",
      "//src/devices/lib/driver",
      "//zircon/public/lib/sync",
      "//zircon/system/ulib/ddk",
    ]
  }

  migrated_manifest("test-$driver-manifest") {
    deps = [ ":test-$driver" ]
  }
}

migrated_manifest("test-board-manifest") {
  deps = [ ":test-board" ]
}

migrated_manifest("test-gpio-manifest") {
  deps = [ ":test-gpio" ]
}

migrated_manifest("test-power-manifest") {
  deps = [ ":test-power" ]
}

migrated_manifest("test-clock-manifest") {
  deps = [ ":test-clock" ]
}

migrated_manifest("test-i2c-manifest") {
  deps = [ ":test-i2c" ]
}

migrated_manifest("test-spi-manifest") {
  deps = [ ":test-spi" ]
}

migrated_manifest("test-audio-codec-manifest") {
  deps = [ ":test-audio-codec" ]
}

migrated_manifest("test-pwm-manifest") {
  deps = [ ":test-pwm" ]
}

unittest_package("platform-bus-package") {
  package_name = "platform-bus"
  deps = [ ":platform-bus" ]

  tests = [
    {
      name = "platform-bus"
      dest = "platform-bus-test"
    },
  ]
}
